import win.ui;
import win.ui.menu;
import inet.whttp;
import web.json;
import console;
import win.ui.shadow;
import web.layout; 
import web.layout.behavior.windowCommand;
import web.layout.behavior.tabs;
/*DSG{{*/
mainForm = ..win.form(text="微信墙";right=480;bottom=298;topmost=1)
mainForm.add()
/*}}*/

wbLayout = web.layout( mainForm )  

if( _STUDIO_INVOKED ){
	import web.layout.debug;
	wbLayout.attachEventHandler( web.layout.debug );
}

wbLayout.go("/layout/main.html") 
var url = "http://114.80.178.78/RoxBusiness/wxqgl/getWxqReplyInfo.3g?wxqid=8&wxNumber=tebon518";//取数地址，聊天室号
var intev = 5;//刷新频率
var templ = '<div class="chat_one"><img src="$personal_photo" class="personal_photo"><div class="show_content"><p class="nick_name">$nick_name</p><p class="show_text">$show_text</p></div><div class="show_time">$show_time</div></div>';

win.ui.shadow(mainForm); //添加阴影边框
//mainForm.transparent(true);
mainForm.show();

//定制右键菜单
var popmenu = win.ui.popmenu(mainForm);
popmenu.addTable({
	{"配置(&S)";function(id){
		//
	}};
	{"全屏开/关(&F)";function(id){
		mainForm.fullscreen(); //退出全屏
		win.setActive(mainForm.hwnd);
	}};
    {/*分隔线*/};
    {"退出(&X)";function(id){
        mainForm.close();
    }};
});

//窗体消息处理函数
mainForm.wndproc = function(hwnd,message,wParam,lParam){
    select( message ) { 
        case 0x205/*_WM_RBUTTONUP*/{
            popmenu.popup() ;                     
        }
        case 0x112/*_WM_SYSCOMMAND*/{
            if(wParam == 0xF030/*_SC_MAXIMIZE*/){//最大化
                mainForm.fullscreen(); //
				win.setActive(mainForm.hwnd);
				return false;
            }
        }
        case 0x100/*_WM_KEYDOWN*/{
        	if(wParam==0x1B/*_VK_ESC*/){
        		mainForm.fullscreen(false); 
        	}
        }
    }
}

//Whttp post
wget=function(url){
	var whttp = inet.whttp();
    var  html = whttp.get(url);//
    result= string.fromto(html,65001,936);//将网页编码转换为中文 
    whttp.close();//关闭网络连接
    return result;
}
//倒计时
var timeMetter = function(date2){
	var date1 = tonumber(time.now());
	var m = math.round(tonumber(date1-date2/1000));
	var u = "秒前";
	if(m>=60){
		m = math.round(m/60);
		u = "分钟前";
		if(m>=60){
			m = math.round(m/60);
			u = "小时前";
			if(m>=24){
				m = math.round(m/24);
				u = "天前";
			}
		}
	}
	return tostring(m)+u;
}

var getLastedHtml = function(url){
	var html = wget(url);
	var tab = web.json.parse(html);
	var tabobj = tab["obj"];
	var chats ="";
	for(k,v in tabobj){
		var tablist = tabobj[k];
		var pb = templ;
		var show_text ;
		if(tablist[1]=='1'){//图片
			show_text = "<img src='"+ tablist[2] +"'/>";
		}else{//文字
			show_text = tablist[2];
		}
		var show_time = timeMetter(tonumber(tablist[3]));
		pb = string.replace(pb,"$personal_photo",tablist[5]);//头像
		pb = string.replace(pb,"$nick_name",tablist[4]);//昵称
		pb = string.replace(pb,"$show_text",show_text);//留言
		pb = string.replace(pb,"$show_time",show_time);//时间
		chats += pb;
	}
	var chat_area = wbLayout.getEle("chat_area");
	chat_area.innerHTML = "";
	chat_area.innerHTML = chats;
};

tmid = mainForm.addtimer(
	0/*毫秒*/,
	function(hwnd,msg,id,tick){//定时执行代码
		getLastedHtml(url);//定时取数
		return intev*1000;
	}
);

return win.loopMessage(); 
